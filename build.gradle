plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'maven-publish'
}

archivesBaseName = rootProject.archives_base_name
group = rootProject.maven_group

repositories {

}

dependencies {
	runtimeOnly include(project(path: ":ThreeDShareware", configuration: "namedElements"))
	runtimeOnly include(project(path: ":TwentyWFourteenInfinite", configuration: "namedElements"))
}

subprojects {
	apply plugin: "fabric-loom"

	archivesBaseName = project.name
	group = project.maven_group

	dependencies {
		implementation rootProject
	}
}

allprojects {
	apply plugin: "java"
	apply plugin: "maven-publish"

	version = rootProject.mod_version

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	repositories {
		maven { url "https://maven.fabricmc.net/" }
		maven { url "https://maven.terraformersmc.com/releases/" }
		maven { url "https://maven.shedaniel.me/" }
		maven { url "https://maven.parchmentmc.org/" }
	}

	dependencies {
		minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
		mappings loom.layered() {
			officialMojangMappings()
			parchment("org.parchmentmc.data:parchment-1.19.2:2022.08.10@zip")
		}
		modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
		modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"

		modImplementation "com.terraformersmc:modmenu:${rootProject.modmenu_version}"

		modApi("me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_config_version}") {
			exclude(group: "net.fabricmc.fabric-api")
		}
	}

	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"
		options.release = 17
	}

	processResources {
		inputs.property "version", project.version

		filesMatching("fabric.mod.json") {
			expand "version": project.version
		}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(jar) {
				builtBy remapJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}